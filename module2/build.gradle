if (isModule2AsApp.toBoolean())
    apply plugin: 'com.android.application'
else
    apply plugin: 'com.android.library'

def config = rootProject.ext.android
def libraries = rootProject.ext.dependencies

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    defaultConfig {
        if (isModule2AsApp.toBoolean())
            applicationId "com.binzee.modularzationtest_java_module2"
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            if (isModule2AsApp.toBoolean())
                manifest.srcFile 'src/main/asApp/AndroidManifest.xml'
            else
                manifest.srcFile 'src/main/asLibrary/AndroidManifest.xml'
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation project(path: ':commonLib')

    //aRouter
    implementation libraries.arouter
    annotationProcessor libraries.arouterCompiler
}